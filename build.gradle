buildscript {
    repositories {
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "https://artifacts.fizzpod.com/public/mvn/"
        }
    }
    dependencies {
        classpath ('com.fizzpod:gradle-plugin-opinion:18.0.3') {
            exclude group: 'com.fizzpod', module: 'gradle-gitignore-plugin'
        }
    }
}

apply plugin: 'com.fizzpod.pater-build'
apply from: 'buildSrc/build.gradle'

sourceSets {
    main {
        java {
            srcDir 'buildSrc/src/main/java'
        }
        resources {
            srcDir 'buildSrc/src/main/resources'
            exclude 'META-INF/gradle-plugins/com.fizzpod.gitignore.properties'
        }
        groovy {
            srcDir 'buildSrc/src/main/groovy'
        }
    }
    test {
        java {
            srcDir 'buildSrc/src/test/java'
        }
        resources {
            srcDir 'buildSrc/src/test/resources'
        }
        groovy {
            srcDir 'buildSrc/src/test/groovy'
        }
    }
}

gitignore {
    merge true
    url 'https://raw.githubusercontent.com/github/gitignore/main/Gradle.gitignore'
    url 'https://raw.githubusercontent.com/github/gitignore/main/Java.gitignore'
}

dependencies {
    api gradleApi()
    api localGroovy()
}


gradlePlugin {

    plugins {
        plugin {
            description = 'Gradle plugin providing a way of configuring default gitignore values.'
            tags.set(['git', 'build', 'gitignore', 'ci'])
            id = 'com.fizzpod.gitignore'
            displayName = 'Gradle Gitignore plugin'
            implementationClass = 'com.fizzpod.gradle.plugins.gitignore.GitignorePlugin'
        }
    }
}
